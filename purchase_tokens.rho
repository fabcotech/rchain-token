
new
  basket,
  revVaultPurseCh,
  priceCh,
  quantityCh,
  publicKeyCh,
  nonceCh,
  bagDataCh,
  returnCh,
  bagIdCh,
  registryUriCh,
  revAddressCh,
  registryLookup(`rho:registry:lookup`),
  stdout(`rho:io:stdout`),
  revAddress(`rho:rev:address`)
in {

  /*
    The 5 following values must be filled with proper values
  */ 
  // Registry URI of the contract
  registryUriCh!!(`rho:id:REGISTRY_URI`) |
  // Unique ID of the token you want to purchase
  bagIdCh!!("BAG_ID") |
  // Per token price, make sure it is accurate
  priceCh!!(PRICEE) |
  // Bag data: Any
  bagDataCh!!(BAG_DATA) |
  // Quantity you want to purchase, make sure enough are available
  quantityCh!!(QUANTITY) |
  // Your public key
  // If the transfer fails, refund will go to the corresponding REV address
  publicKeyCh!!("PUBLIC_KEY") |
  // A unique nonce to be changed on each operation
  nonceCh!!("BAG_NONCE") |

  registryLookup!(`rho:rchain:revVault`, *revVaultPurseCh) |

  /*
    Create a vault/purse that is just used once (purse)
  */
  for(@(_, *RevVaultPurse) <- revVaultPurseCh) {
    new unf, purseRevAddrCh, purseAuthKeyCh, vaultCh, revAddressCh in {
      revAddress!("fromUnforgeable", *unf, *purseRevAddrCh) |
      RevVaultPurse!("unforgeableAuthKey", *unf, *purseAuthKeyCh) |
      for (@purseAuthKey <- purseAuthKeyCh; @purseRevAddr <- purseRevAddrCh) {

        stdout!({"new purse rev addr": purseRevAddr, "purse authKey": purseAuthKey}) |

        RevVaultPurse!("findOrCreate", purseRevAddr, *vaultCh) |

        for (
          @(true, *vault) <- vaultCh;
          @publicKey <- publicKeyCh;
          @nonce <- nonceCh;
          @bagId <- bagIdCh;
          @registryUri <- registryUriCh;
          @price <- priceCh;
          @bagData <- bagDataCh;
          @quantity <- quantityCh
        ) {

          revAddress!("fromPublicKey", publicKey.hexToBytes(), *revAddressCh) |

          new RevVaultCh in {

            registryLookup!(`rho:rchain:revVault`, *RevVaultCh) |
            for (@(_, RevVault) <- RevVaultCh; deployerRevAddress <- revAddressCh) {

              stdout!(("3.transfer_funds.rho")) |

              /*
                Put price * quantity REV in the purse
              */
              match (
                *deployerRevAddress,
                purseRevAddr,
                price * quantity
              ) {
                (from, to, amount) => {

                  new vaultCh, revVaultkeyCh, deployerId(`rho:rchain:deployerId`) in {
                    @RevVault!("findOrCreate", from, *vaultCh) |
                    @RevVault!("deployerAuthKey", *deployerId, *revVaultkeyCh) |
                    for (@(true, vault) <- vaultCh; key <- revVaultkeyCh) {

                      stdout!(("Beginning transfer of ", amount, "REV from", from, "to", to)) |

                      new resultCh, entryCh in {
                        @vault!("transfer", to, amount, *key, *resultCh) |
                        for (@result <- resultCh) {

                          stdout!(("Finished transfer of ", amount, "REV to", to, "result was:", result)) |
                          match result {
                            (true, Nil) => {
                              stdout!("yes") |
                              registryLookup!(registryUri, *entryCh) |

                              for(entry <- entryCh) {
                                stdout!(("GET ENTRY", *entry)) |
                                entry!(
                                  {
                                    "type": "PURCHASE_TOKENS",
                                    "payload": {
                                      "quantity": quantity,
                                      "bagId": bagId,
                                      "data": bagData,
                                      "nonce": nonce,
                                      "publicKey": publicKey,
                                      "purseRevAddr": purseRevAddr,
                                      "purseAuthKey": purseAuthKey
                                    }
                                  },
                                  *returnCh
                                ) |
                                for (resp <- returnCh) {
                                  match *resp {
                                    true => {
                                      basket!({ "status": "completed" }) |
                                      stdout!("completed, tokens purchased")
                                    }
                                    _ => {
                                      basket!({ "status": "failed", "message": *resp }) |
                                      stdout!(("failed", *resp))
                                    }
                                  }
                                }
                              }
                            }
                            _ => {
                              basket!({ "status": "failed", "message": result }) |
                              stdout!(("failed", result))
                            }
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}
